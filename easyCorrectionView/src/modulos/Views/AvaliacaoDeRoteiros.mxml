<?xml version="1.0" encoding="utf-8"?>

<les:ModuleTitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" 
						xmlns:les="modulos.sistema.*" 
						xmlns:comp="com.adobe.flex.extras.controls.*"
	width="800" height="450" creationComplete="init()" title="Avaliação de Roteiros" showCloseButton="true" close="fechaJanela()" layout="absolute">
		
		<mx:Script>
			<![CDATA[
				import bean.Utilitarios.Constantes;
				import bean.Utilitarios.Util;
				import bean.avaliacoes.Avaliacao;
				import bean.roteiros.Submissao;
				import bean.roteiros.EquipeHasUsuarioHasRoteiro;
				import bean.Utilitarios.CastDeEntidades;
				import bean.roteiros.Roteiro;
				import mx.collections.ArrayCollection;
				import mx.utils.ArrayUtil;
				import eventos.FechaPopupEvent;
				import mx.utils.StringUtil;
				import Scripts.ControladorDeModulos;
				import mx.binding.utils.BindingUtils;
				import mx.rpc.events.ResultEvent;
				import mx.rpc.events.FaultEvent;
				import com.flexspy.imp.Utils;
				import mx.effects.Rotate;
				import mx.controls.Image;
				import flash.net.navigateToURL;
				
				[Bindable]
	            [Embed(source="../../image/atencao.gif")]
	            public var atencao:Class;
	
	            [Bindable]
	            [Embed(source="../../image/sucessoCorrecao.png")]
	            public var sucessoCorrecao:Class;
				
				
				private const FILE_UPLOAD_URL:String = Constantes.urlUploadDonwload;
				private var fileRef:FileReference = new FileReference();
				private var request:URLRequest = new URLRequest(ExternalInterface.call('downLoadRoteiro'));
				private var nomeArquivoCodigo: String = "";
				
				private var eur:EquipeHasUsuarioHasRoteiro = new EquipeHasUsuarioHasRoteiro();
				private var avaliacao:Avaliacao = new Avaliacao();
					            
	            private function falha(event:FaultEvent):void {
					var mensagem:String;
					try{
						if (event.fault.rootCause.message == null) {
							mensagem = event.fault.faultString;
						} else {
							mensagem = event.fault.rootCause.message;
						}
					}
					catch(e:Error){
						mensagem = "Serviço indisponível, reinicie a operação.";
					}
					ControladorDeModulos.criaModulo("modulos/sistema/MensagemStatus.swf", this, new Array(true, mensagem));
				}
				
				private function fechaJanela():void {
					dispatchEvent(new FechaPopupEvent(this));
				}
				
				/*
				 *	Caso aconteça algum erro no acesso ao controlador, através do HttpService
				 *	então devemos mostra uma mensagem de erro
				 */
				private function lancaEvento(event:FaultEvent):void {
					var e:FaultEvent = new FaultEvent(FaultEvent.FAULT, true, true,
						event.fault, event.token, event.message);
					dispatchEvent(e);
				}
				
				private function init():void {
					
					facade.channelSet = ControladorDeModulos.criaCanal();
					var estado: String = String(ControladorDeModulos.getParam());
					var roteiros: ArrayCollection = new ArrayCollection();
					listarRoteiros();
				}
				
				private function listarRoteiros():void{
					facade.getOperation("listarRoteiros").send();
				}
				
				private function listarRoteiros_result(event:ResultEvent):void{
					comboRoteiros.dataProvider = new ArrayCollection();
					comboRoteiros.selectedIndex = -1;
					comboRoteiros.dataProvider = event.result;
					
				}				
				
				private function pegaEquipesPorRoteiro():void{
					var rot: Roteiro = CastDeEntidades.castRoteiro(comboRoteiros.selectedItem);
					inputPeso.text = String(100 - rot.porcentagemTestesAutomaticos) + "%";
					var idUsuario: int = SharedObject.getLocal("sistema", "/").data.idUsuario;
					facade.getOperation("getAvaliacaoPorRoteiroEquipeUnicos").send(rot, idUsuario);
				}
				
				private function getAvaliacaoPorRoteiroEquipeUnicos_result(event:ResultEvent):void{
					listaEquipes.dataProvider = new ArrayCollection(); 
					listaEquipes.dataProvider = event.result;
				}
				
				private function selecaoLabelEquipes(item: *): String{
					return item.submissao.equipeHasUsuarioHasRoteiro.equipe.nome;
				}
				
				private function selecionaEquipe():void{
					avaliacao = CastDeEntidades.castAvaliacao(listaEquipes.selectedItem);
					eur = avaliacao.submissao.equipeHasUsuarioHasRoteiro;
					facade.getOperation("getEquipeHasUsuarioHasRoteiroPorEquipeERoteiro").send(eur.equipe.id, eur.roteiro.id);
				}
				
				private function getEquipeHasUsuarioHasRoteiroPorEquipeERoteiro_result(event:ResultEvent):void{
					listaIntegrantes.dataProvider = new ArrayCollection(); 
					listaIntegrantes.dataProvider = event.result;
					try{
						var nota: Number = avaliacao.notaCorrecao;
						if(nota == 10){
							inputNota.text = String(nota);
						}else{
							inputNota.text = "0" + String(nota);
						}
					}
					catch(e: Error){
						inputNota.text = "0000";
					}
					facade.getOperation("getNomeArquivoCodigo").send(avaliacao.submissao);
					
				}
					
				private function selecaoLabelIntegrantes(item: *): String{
					return item.usuario.nome;
				}
				
				private function getNomeArquivoCodigo_result(event:ResultEvent):void{
					nomeArquivoCodigo = String(event.result);
					baixarImplementacao.enabled = true;
					botaoSalvar.enabled = true;
				}
				
				private function baixarCodigo():void{
					try{
						var variables:URLVariables = new URLVariables();
						variables.nomeArquivo = avaliacao.submissao.url + nomeArquivoCodigo;
						request.data = variables;
						fileRef.download(request, nomeArquivoCodigo);		
					}
					catch(e: Error){
						var mensagem:String = "Selecione Uma Equipe.";
						ControladorDeModulos.criaModulo("modulos/sistema/MensagemStatus.swf", this, new Array(true, mensagem));
					}
				}
				
				
				private function salvarNotaCorretor():void{
					if (Util.formataTextValorToNumber2(inputNota.fullText) < 10){
						avaliacao.notaCorrecao = Util.formataTextValorToNumber2(inputNota.fullText);
						avaliacao.corrigido = true;
						facade.getOperation("salvarAvaliacao").send(avaliacao);
					}
					else{
						var mensagem:String = "A nota deve ser menor que 10.";
						ControladorDeModulos.criaModulo("modulos/sistema/MensagemStatus.swf", this, new Array(true, mensagem));
					}
				}
				
				private function salvarAvaliacao_result(event:ResultEvent):void{
					var mensagem:String = "Equipe Avaliada Com Sucesso.";
					ControladorDeModulos.criaModulo("modulos/sistema/MensagemStatus.swf", this, new Array(false, mensagem));
					pegaEquipesPorRoteiro();	
				}
				
				private function defIcone(item:Object):Class {
	                var iconClass:Class;
	                if (item.corrigida) {
	                    iconClass = sucessoCorrecao;
	                } else {
	                    iconClass = atencao;
	                }
	                return iconClass;
	            }
				
			]]>
		</mx:Script>
				
		<mx:RemoteObject id="facade" destination="easyCorrection" showBusyCursor="true">
			<mx:method name="listarRoteiros" result="listarRoteiros_result(event)" fault="falha(event)"/>
			<mx:method name="getEquipeHasUsuarioHasRoteiroPorEquipeERoteiro" result="getEquipeHasUsuarioHasRoteiroPorEquipeERoteiro_result(event)" fault="falha(event)"/>
			<mx:method name="getAvaliacaoPorRoteiroEquipeUnicos" result="getAvaliacaoPorRoteiroEquipeUnicos_result(event)" fault="falha(event)"/>
			<mx:method name="getNomeArquivoCodigo" result="getNomeArquivoCodigo_result(event)" fault="falha(event)"/>
			<mx:method name="salvarAvaliacao" result="salvarAvaliacao_result(event)" fault="falha(event)"/>
		</mx:RemoteObject>
		
		<mx:Label text="Roteiro:" fontWeight="bold"
			textAlign="left" width="64" x="10" y="22" id="labelRoteiro"/>
		<mx:Label text="Equipes:" fontWeight="bold"
			textAlign="left" width="72" x="10" y="72" id="labelGrupos"/>
		<mx:Label text="Integrantes: " fontWeight="bold"
			textAlign="left" width="93.04999" x="265.95" y="72" id="labelIntegrantes" height="20"/>
		<comp:ComboBoxAC labelField="nome" x="72" y="21" width="698" id="comboRoteiros" change="pegaEquipesPorRoteiro()"/>
		<mx:HRule x="10" y="62" width="760" height="2"/>
		<mx:HRule x="251" y="234" width="518" height="2"/>
		<mx:List id="listaEquipes" x="10" y="94" width="226" height="302" labelFunction="selecaoLabelEquipes" itemClick="selecionaEquipe()" iconFunction="defIcone" fontSize="12" color="#042E8E"/>
		<mx:List id="listaIntegrantes" x="265.95" y="94" width="504.05" height="66" labelFunction="selecaoLabelIntegrantes"/>
		<mx:VRule x="244" y="62" height="334" width="13.95"/>
		<mx:Button x="398" y="179" label="Baixar Implementação" click="baixarCodigo()" width="239" height="33" fontSize="16" fontStyle="italic" fontWeight="normal" id="baixarImplementacao" enabled="false"/>
		<mx:Button x="398" y="329" label="Salvar Nota" click="salvarNotaCorretor()" width="239" height="33" fontSize="16" fontStyle="italic" fontWeight="normal" enabled="false" id="botaoSalvar"/>
		<mx:Label text="Nota:" fontWeight="bold"
			textAlign="left" width="58" x="398" y="275" id="labelNota" fontSize="16"/>
		<mx:Label text="Peso:" fontWeight="bold"
			textAlign="left" width="49" x="541" y="278" id="labelPeso"/>
		<mx:Label fontWeight="bold" textAlign="left" width="49" x="588" y="278" id="inputPeso" height="23"/>
		<comp:MaskedTextInput x="464" y="276" inputMask="##,##" width="69" id="inputNota" color="#1B7A0F" text="0"/>
		
		
</les:ModuleTitleWindow>
